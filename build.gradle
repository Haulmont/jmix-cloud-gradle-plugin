/*
 * Copyright 2021 Haulmont.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.15.0'
}

group = 'io.jmix.cloud.gradle'

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation localGroovy()

    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    implementation platform('software.amazon.awssdk:bom:2.16.69')
    implementation 'software.amazon.awssdk:ec2'
    implementation 'software.amazon.awssdk:ssm'
    implementation 'com.jcraft:jsch:0.1.55'
    implementation 'com.github.docker-java:docker-java:3.2.8'
    implementation 'com.github.docker-java:docker-java-transport-httpclient5:3.2.8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

gradlePlugin {
    plugins {
        jmixCloudPlugin {
            id = 'io.jmix.cloud'
            implementationClass = 'io.jmix.cloud.gradle.JmixCloudPlugin'
        }
    }
}

pluginBundle {
    website = 'http://jmix.io'
    vcsUrl = 'https://github.com/Haulmont/jmix-all'
    description = 'Jmix Framework Cloud Gradle Plugin'
    tags = ['jmix', 'spring', 'cloud']

    plugins {
        jmixCloudPlugin {
            displayName = 'Jmix Framework Cloud Gradle Plugin'
        }
    }
}

publishing {
    if (rootProject.hasProperty('jmixUploadUrl')) {
        repositories {
            maven {
                url = rootProject['jmixUploadUrl']
                credentials {
                    username rootProject['jmixUploadUser']
                    password rootProject['jmixUploadPassword']
                }
                allowInsecureProtocol = true
            }
        }
    }
}
